WHITESPACE = _{" "|"\n"}
modifier = {"|" ~ identifyer ~ argument*}
argument = { ":" ~ value }
calculated = {!"\\" ~ "{" ~ calculated_value ~ "}"}
calculated_value = { value ~ modifier* }
identifyer = @{!(key_words) ~ 'a'..'z' ~ ('A'..'Z'|'a'..'z'|'0'..'9'|"_")*}
text = {(!"{" ~ ("\\{" | ANY))+}

// Template
tempalte = ${SOI ~ template_content ~ EOI}
template_content = { (calculated|text|conditional)* }

// Values
string = { "\"" ~ inner_string ~ "\"" }
inner_string = {(!"\"" ~ ( "\\\""| ANY))*}
number = { ("+"|"-")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
boolean = {true_key_word|false_key_word}
value = { boolean|identifyer|number|string }

// Blocks
conditional = { 
    "{" ~ if_key_word ~ condition ~ "}" ~
        template_content ~
    (
        "{" ~ else_key_word ~ "}" ~
            template_content
    )? ~
    "{" ~ endif_key_word ~ "}"
}

// Condition
condition = {
    ("(" ~ (condition) ~ ")") |
    (
        (
            compare_condition |
            calculated_value
        ) ~ 
        (
            (and_operator|or_operator) ~
            condition
        )*
    )
}

compare_condition = { calculated_value ~ compare_operator ~ calculated_value }

compare_operator = { 
    eq_operator|
    ne_operator|
    lt_operator|
    le_operator|
    gt_operator|
    ge_operator
}
eq_operator = { "==" }
ne_operator = { "!=" }
lt_operator = { "<" }
le_operator = { "<=" }
gt_operator = { ">" }
ge_operator = { ">=" }

and_operator = {"&&"}
or_operator = {"||"}

// Keywords
if_key_word = _{"if"}
else_key_word = _{"else"}
endif_key_word = _{"endif"}
true_key_word = _{"true"}
false_key_word = _{"false"}
key_words = { if_key_word | else_key_word | endif_key_word | true_key_word | false_key_word }